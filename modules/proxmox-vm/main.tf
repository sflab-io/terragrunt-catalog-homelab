data "homelab_naming" "this" {
  env = var.env
  app = var.app
}

data "local_file" "ssh_public_key" {
  filename = var.ssh_public_key_path
}

resource "proxmox_virtual_environment_vm" "this" {
  name      = data.homelab_naming.this.name
  node_name = "pve1"

  clone {
    vm_id = 9002
  }

  agent {
    # NOTE: The agent is installed and enabled as part of the cloud-init configuration in the template VM, see cloud-config.tf
    # The working agent is *required* to retrieve the VM IP addresses.
    # If you are using a different cloud-init configuration, or a different clone source
    # that does not have the qemu-guest-agent installed, you may need to disable the `agent` below and remove the `vm_ipv4_address` output.
    # See https://registry.terraform.io/providers/bpg/proxmox/latest/docs/resources/virtual_environment_vm#qemu-guest-agent for more details.
    enabled = true
  }

  cpu {
    cores = var.cores
  }

  memory {
    dedicated = var.memory
  }

  network_device {
    bridge = var.network_bridge
  }

  disk {
    size = var.disk_size
  }

  initialization {
    dynamic "dns" {
      for_each = var.network_config.type == "static" && length(var.network_config.dns_servers) > 0 ? [1] : []
      content {
        domain  = var.network_config.domain
        servers = var.network_config.dns_servers
      }
    }

    ip_config {
      ipv4 {
        address = var.network_config.type == "dhcp" ? "dhcp" : "${var.network_config.ip_address}/${var.network_config.cidr}"
        gateway = var.network_config.type == "static" ? var.network_config.gateway : null
      }
    }

    user_account {
      username = var.username
      keys     = [trimspace(data.local_file.ssh_public_key.content)]
    }
  }
}

resource "proxmox_virtual_environment_pool_membership" "this" {
  count = var.pool_id != "" ? 1 : 0

  pool_id = var.pool_id
  vm_id   = proxmox_virtual_environment_vm.this.vm_id
}
